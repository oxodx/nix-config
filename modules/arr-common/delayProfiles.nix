{ serviceName }:
{
  config,
  lib,
  pkgs,
  ...
}:
with lib;
let
  cfg = config.nixflix.${serviceName};
  inherit (import ./utils.nix { inherit lib pkgs serviceName; })
    usesMediaDirs
    capitalizedName
    mkSecureCurl
    ;

  defaultDelayProfile = {
    enableUsenet = true;
    enableTorrent = true;
    preferredProtocol = "usenet";
    usenetDelay = 0;
    torrentDelay = 0;
    bypassIfHighestQuality = true;
    bypassIfAboveCustomFormatScore = false;
    minimumCustomFormatScore = 0;
    order = 2147483647;
    tags = [ ];
    id = 1;
  };
in
{
  options.nixflix.${serviceName}.config = optionalAttrs usesMediaDirs {
    delayProfiles = mkOption {
      type = types.listOf (
        types.submodule {
          options = {
            id = mkOption {
              type = types.int;
              description = "Unique identifier for the delay profile";
            };
            enableUsenet = mkOption {
              type = types.bool;
              default = true;
              description = "Enable Usenet protocol for this profile";
            };
            enableTorrent = mkOption {
              type = types.bool;
              default = true;
              description = "Enable Torrent protocol for this profile";
            };
            preferredProtocol = mkOption {
              type = types.enum [
                "usenet"
                "torrent"
              ];
              default = "usenet";
              description = "Preferred download protocol when both are available";
            };
            usenetDelay = mkOption {
              type = types.int;
              default = 0;
              description = "Delay in minutes before grabbing a Usenet release";
            };
            torrentDelay = mkOption {
              type = types.int;
              default = 0;
              description = "Delay in minutes before grabbing a Torrent release";
            };
            bypassIfHighestQuality = mkOption {
              type = types.bool;
              default = true;
              description = "Bypass delay if release is the highest quality available";
            };
            bypassIfAboveCustomFormatScore = mkOption {
              type = types.bool;
              default = false;
              description = "Bypass delay if custom format score is above minimum";
            };
            minimumCustomFormatScore = mkOption {
              type = types.int;
              default = 0;
              description = "Minimum custom format score to bypass delay";
            };
            order = mkOption {
              type = types.int;
              default = 50;
              description = "Order/priority of this delay profile (lower values = higher priority)";
            };
            tags = mkOption {
              type = types.listOf types.int;
              default = [ ];
              description = "List of tag IDs this delay profile applies to (empty = applies to all)";
            };
          };
        }
      );
      default = [ defaultDelayProfile ];
      defaultText = literalExpression ''
        [
          {
            enableUsenet = true;
            enableTorrent = true;
            preferredProtocol = "usenet";
            usenetDelay = 0;
            torrentDelay = 0;
            bypassIfHighestQuality = true;
            bypassIfAboveCustomFormatScore = false;
            minimumCustomFormatScore = 0;
            order = 2147483647;
            tags = [];
            id = 1;
          };
        ]
      '';
      description = ''
        List of delay profiles to configure via the API /delayprofile endpoint.

        Profiles are created/updated in id order. If no profile with id=1 is provided,
        a default profile will be added automatically.
      '';
    };
  };

  config = mkIf (usesMediaDirs && config.nixflix.enable && cfg.enable && cfg.config.apiKey != null) {
    systemd.services."${serviceName}-delayprofiles" =
      let
        userProfileIds = map (p: p.id) cfg.config.delayProfiles;
        hasDefaultProfile = elem 1 userProfileIds;
        mergedProfiles =
          if hasDefaultProfile then
            cfg.config.delayProfiles
          else
            [ defaultDelayProfile ] ++ cfg.config.delayProfiles;
        sortedProfiles = sort (a: b: a.id < b.id) mergedProfiles;
      in
      {
        description = "Configure ${serviceName} delay profiles via API";
        after = [ "${serviceName}-config.service" ];
        requires = [ "${serviceName}-config.service" ];
        wantedBy = [ "multi-user.target" ];

        serviceConfig = {
          Type = "oneshot";
          RemainAfterExit = true;
        };

        script = ''
          set -eu

          BASE_URL="http://${cfg.config.hostConfig.bindAddress}:${builtins.toString cfg.config.hostConfig.port}${cfg.config.hostConfig.urlBase}/api/${cfg.config.apiVersion}"

          echo "Fetching existing delay profiles..."
          DELAY_PROFILES=$(${
            mkSecureCurl cfg.config.apiKey {
              url = "$BASE_URL/delayprofile";
              extraArgs = "-Sf";
            }
          } 2>/dev/null)

          CONFIGURED_IDS=$(cat <<'EOF'
          ${builtins.toJSON (map (p: p.id) sortedProfiles)}
          EOF
          )

          echo "Removing delay profiles not in configuration..."
          echo "$DELAY_PROFILES" | ${pkgs.jq}/bin/jq -r '.[] | @json' | while IFS= read -r profile; do
            PROFILE_ID=$(echo "$profile" | ${pkgs.jq}/bin/jq -r '.id')

            if ! echo "$CONFIGURED_IDS" | ${pkgs.jq}/bin/jq -e --argjson id "$PROFILE_ID" 'index($id)' >/dev/null 2>&1; then
              echo "Deleting delay profile not in config (ID: $PROFILE_ID)"
              ${
                mkSecureCurl cfg.config.apiKey {
                  url = "$BASE_URL/delayprofile/$PROFILE_ID";
                  method = "DELETE";
                  extraArgs = "-Sf";
                }
              } >/dev/null 2>&1 || echo "Warning: Failed to delete delay profile $PROFILE_ID (may be in use)"
            fi
          done

          ${concatMapStringsSep "\n" (
            profileConfig:
            let
              profileJson = builtins.toJSON profileConfig;
              profileId = toString profileConfig.id;
            in
            ''
              echo "Processing delay profile (ID: ${profileId})..."

              EXISTING_PROFILE=$(echo "$DELAY_PROFILES" | ${pkgs.jq}/bin/jq -r '.[] | select(.id == ${profileId}) | @json' || echo "")

              if [ -n "$EXISTING_PROFILE" ]; then
                echo "Delay profile ${profileId} already exists, updating..."
                ${
                  mkSecureCurl cfg.config.apiKey {
                    url = "$BASE_URL/delayprofile/${profileId}";
                    method = "PUT";
                    headers = {
                      "Content-Type" = "application/json";
                    };
                    data = profileJson;
                    extraArgs = "-Sf";
                  }
                } > /dev/null
                echo "Delay profile ${profileId} updated"
              else
                echo "Delay profile ${profileId} does not exist, creating..."
                ${
                  mkSecureCurl cfg.config.apiKey {
                    url = "$BASE_URL/delayprofile";
                    method = "POST";
                    headers = {
                      "Content-Type" = "application/json";
                    };
                    data = profileJson;
                    extraArgs = "-Sf";
                  }
                } > /dev/null
                echo "Delay profile ${profileId} created"
              fi
            ''
          ) sortedProfiles}

          echo "${capitalizedName} delay profiles configuration complete"
        '';
      };
  };
}
