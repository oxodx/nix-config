{
  config,
  lib,
  pkgs,
  ...
}:
with lib;
let
  cfg = config.nixflix.prowlarr;
  secrets = import ../../lib/secrets { inherit lib; };

  mkSecureCurl = import ../../lib/mk-secure-curl.nix { inherit lib pkgs; };
in
{
  options.nixflix.prowlarr.config.indexers = mkOption {
    type = types.listOf (
      types.submodule {
        freeformType = types.attrsOf types.anything;
        options = {
          name = mkOption {
            type = types.str;
            description = "Name of the Prowlarr Indexer Schema";
          };
          apiKey = secrets.mkSecretOption {
            description = "API key for the indexer. Applied to schema fields named `apikey` or `apiKey`.";
            nullable = true;
          };
          apikey = secrets.mkSecretOption {
            description = "API key for the indexer (lowercase variant). Applied to schema fields named `apikey` or `apiKey`.";
            nullable = true;
          };
          username = secrets.mkSecretOption {
            description = "Username for the indexer.";
            nullable = true;
          };
          password = secrets.mkSecretOption {
            description = "Password for the indexer.";
            nullable = true;
          };
          passkey = secrets.mkSecretOption {
            description = "Passkey for the indexer.";
            nullable = true;
          };
          appProfileId = mkOption {
            type = types.int;
            default = 1;
            description = "Application profile ID for the indexer (default: 1).";
          };
          tags = mkOption {
            type = types.listOf types.str;
            default = [ ];
            description = ''
              Use tags to specify Indexer Proxies or which apps the indexer is synced to.

              Tags should be used with caution, they can have unintended effects. An indexer with a tag will only sync to apps with the same tag.
            '';
          };
        };
      }
    );
    default = [ ];
    description = ''
      List of indexers to configure in Prowlarr. Prowlarr supports many indexers in addition to any indexer that uses the Newznab/Torznab standard using 'Generic Newznab' (for usenet) or 'Generic Torznab' (for torrents).

      Any additional attributes beyond `name`, `apiKey`, `apikey`, `username`, `password`, `passkey`, and `appProfileId`
      will be applied as field values to the indexer schema.

      The `apiKey` or `apikey` value is automatically applied to whichever field name the indexer schema
      uses — some schemas use `apiKey` (camelCase) and others use `apikey` (all-lowercase).

      You can run the following command to get the field names for a particular indexer:

      ```sh
      curl -s -H "X-Api-Key: $(sudo cat </path/to/prowlarr/apiKey>)" "http://127.0.0.1:9696/api/v1/indexer/schema" | jq '.[] | select(.name=="<indexerName>") | .fields'
      ```
    '';
  };

  config.systemd.services."prowlarr-indexers" =
    mkIf (config.nixflix.enable && cfg.enable && cfg.config.apiKey != null)
      {
        description = "Configure Prowlarr indexers via API";
        after = [
          "prowlarr.service"
          "prowlarr-config.service"
          "prowlarr-tags.service"
        ];
        requires = [
          "prowlarr.service"
          "prowlarr-config.service"
          "prowlarr-tags.service"
        ];
        wantedBy = [ "multi-user.target" ];

        serviceConfig = {
          Type = "oneshot";
          RemainAfterExit = true;
        };

        script = ''
          set -eu

          BASE_URL="http://${cfg.connectionAddress}:${builtins.toString cfg.config.hostConfig.port}${cfg.config.hostConfig.urlBase}/api/${cfg.config.apiVersion}"

          # Fetch all indexer schemas
          echo "Fetching indexer schemas..."
          SCHEMAS=$(${
            mkSecureCurl cfg.config.apiKey {
              url = "$BASE_URL/indexer/schema";
              extraArgs = "-S";
            }
          })

          # Fetch existing indexers
          echo "Fetching existing indexers..."
          INDEXERS=$(${
            mkSecureCurl cfg.config.apiKey {
              url = "$BASE_URL/indexer";
              extraArgs = "-S";
            }
          })

          # Fetch all tags for name-to-ID resolution
          echo "Fetching tags..."
          ALL_TAGS=$(${
            mkSecureCurl cfg.config.apiKey {
              url = "$BASE_URL/tag";
              extraArgs = "-S";
            }
          })

          # Build list of configured indexer names
          CONFIGURED_NAMES=$(cat <<'EOF'
          ${builtins.toJSON (map (i: i.name) cfg.config.indexers)}
          EOF
          )

          # Delete indexers that are not in the configuration
          echo "Removing indexers not in configuration..."
          echo "$INDEXERS" | ${pkgs.jq}/bin/jq -r '.[] | @json' | while IFS= read -r indexer; do
            INDEXER_NAME=$(echo "$indexer" | ${pkgs.jq}/bin/jq -r '.name')
            INDEXER_ID=$(echo "$indexer" | ${pkgs.jq}/bin/jq -r '.id')

            if ! echo "$CONFIGURED_NAMES" | ${pkgs.jq}/bin/jq -e --arg name "$INDEXER_NAME" 'index($name)' >/dev/null 2>&1; then
              echo "Deleting indexer not in config: $INDEXER_NAME (ID: $INDEXER_ID)"
              ${
                mkSecureCurl cfg.config.apiKey {
                  url = "$BASE_URL/indexer/$INDEXER_ID";
                  method = "DELETE";
                  extraArgs = "-Sf";
                }
              } >/dev/null || echo "Warning: Failed to delete indexer $INDEXER_NAME"
            fi
          done

          # Run each indexer in its own subshell and fold its exit status into RC, so a
          # single failing indexer doesn't abort the rest of the batch; exit non-zero at
          # the end if any failed, so Restart=on-failure can retry the stragglers.
          RC=0
          set +e

          ${concatMapStringsSep "\n" (
            indexerConfig:
            let
              indexerName = indexerConfig.name;
              inherit (indexerConfig)
                apiKey
                apikey
                username
                password
                passkey
                ;
              allOverrides = builtins.removeAttrs indexerConfig [
                "name"
                "apiKey"
                "apikey"
                "username"
                "password"
                "passkey"
                "tags"
              ];
              fieldOverrides = lib.filterAttrs (
                name: value: value != null && !lib.hasPrefix "_" name
              ) allOverrides;
              fieldOverridesJson = builtins.toJSON fieldOverrides;

              jqSecrets = secrets.mkJqSecretArgs {
                apiKey = if apiKey == null then "" else apiKey;
                apikey = if apikey == null then "" else apikey;
                username = if username == null then "" else username;
                password = if password == null then "" else password;
                passkey = if passkey == null then "" else passkey;
              };
            in
            ''
              (
              set -e
              echo "Processing indexer: ${indexerName}"

              apply_field_overrides() {
                local indexer_json="$1"
                local overrides="$2"

                echo "$indexer_json" | ${pkgs.jq}/bin/jq \
                  ${jqSecrets.flagsString} \
                  --argjson overrides "$overrides" '
                    .fields[] |= (
                      if (.name == "apiKey" or .name == "apikey") then
                        if ${jqSecrets.refs.apiKey} != "" then .value = ${jqSecrets.refs.apiKey}
                        elif ${jqSecrets.refs.apikey} != "" then .value = ${jqSecrets.refs.apikey}
                        else .
                        end
                      elif .name == "username" and ${jqSecrets.refs.username} != "" then .value = ${jqSecrets.refs.username}
                      elif .name == "password" and ${jqSecrets.refs.password} != "" then .value = ${jqSecrets.refs.password}
                      elif .name == "passkey" and ${jqSecrets.refs.passkey} != "" then .value = ${jqSecrets.refs.passkey}
                      else .
                      end
                    )
                    | . + $overrides
                    | .fields[] |= (
                        . as $field |
                        if $overrides[$field.name] != null then
                          .value = $overrides[$field.name]
                        else
                          .
                        end
                      )
                  '
              }

              FIELD_OVERRIDES=${escapeShellArg fieldOverridesJson}

              EXISTING_INDEXER=$(echo "$INDEXERS" | ${pkgs.jq}/bin/jq -r --arg name ${escapeShellArg indexerName} '.[] | select(.name == $name) | @json' || echo "")

              if [ -n "$EXISTING_INDEXER" ]; then
                echo "Indexer ${indexerName} already exists, updating..."
                INDEXER_ID=$(echo "$EXISTING_INDEXER" | ${pkgs.jq}/bin/jq -r '.id')

                UPDATED_INDEXER=$(apply_field_overrides "$EXISTING_INDEXER" "$FIELD_OVERRIDES")

                TAG_IDS=$(echo "$ALL_TAGS" | ${pkgs.jq}/bin/jq --argjson names ${escapeShellArg (builtins.toJSON indexerConfig.tags)} \
                  '[.[] | select(.label as $l | $names | index($l)) | .id]')
                UPDATED_INDEXER=$(echo "$UPDATED_INDEXER" | ${pkgs.jq}/bin/jq --argjson tags "$TAG_IDS" '.tags = $tags')

                RESPONSE_FILE=$(mktemp)
                HTTP_CODE=$(
                  ${mkSecureCurl cfg.config.apiKey {
                    url = "$BASE_URL/indexer/$INDEXER_ID?forceSave=true";
                    method = "PUT";
                    headers = {
                      "Content-Type" = "application/json";
                    };
                    data = "$UPDATED_INDEXER";
                    extraArgs = "-S -o \"$RESPONSE_FILE\" -w \"%{http_code}\"";
                  }}
                )
                if [ "$HTTP_CODE" -ge 400 ]; then
                  echo "Error updating indexer ${indexerName} (HTTP $HTTP_CODE):"
                  cat "$RESPONSE_FILE"
                  rm -f "$RESPONSE_FILE"
                  exit 1
                fi
                rm -f "$RESPONSE_FILE"
                echo "Indexer ${indexerName} updated"
              else
                echo "Indexer ${indexerName} does not exist, creating..."

                SCHEMA=$(echo "$SCHEMAS" | ${pkgs.jq}/bin/jq -r --arg name ${escapeShellArg indexerName} '.[] | select(.name == $name) | @json' || echo "")

                if [ -z "$SCHEMA" ]; then
                  echo "Error: No schema found for indexer ${indexerName}"
                  exit 1
                fi

                NEW_INDEXER=$(apply_field_overrides "$SCHEMA" "$FIELD_OVERRIDES")
                NEW_INDEXER=$(echo "$NEW_INDEXER" | ${pkgs.jq}/bin/jq '
                  (.indexerUrls[0] // null) as $firstUrl |
                  if $firstUrl != null then
                    .fields[] |= (
                      if .name == "baseUrl" and (.value == null or .value == "") then
                        .value = $firstUrl
                      else .
                      end
                    )
                  else .
                  end
                ')

                TAG_IDS=$(echo "$ALL_TAGS" | ${pkgs.jq}/bin/jq --argjson names ${escapeShellArg (builtins.toJSON indexerConfig.tags)} \
                  '[.[] | select(.label as $l | $names | index($l)) | .id]')
                NEW_INDEXER=$(echo "$NEW_INDEXER" | ${pkgs.jq}/bin/jq --argjson tags "$TAG_IDS" '.tags = $tags')

                RESPONSE_FILE=$(mktemp)
                HTTP_CODE=$(
                  ${mkSecureCurl cfg.config.apiKey {
                    url = "$BASE_URL/indexer?forceSave=true";
                    method = "POST";
                    headers = {
                      "Content-Type" = "application/json";
                    };
                    data = "$NEW_INDEXER";
                    extraArgs = "-S -o \"$RESPONSE_FILE\" -w \"%{http_code}\"";
                  }}
                )
                if [ "$HTTP_CODE" -ge 400 ]; then
                  echo "Error creating indexer ${indexerName} (HTTP $HTTP_CODE):"
                  cat "$RESPONSE_FILE"
                  rm -f "$RESPONSE_FILE"
                  exit 1
                fi
                rm -f "$RESPONSE_FILE"
                echo "Indexer ${indexerName} created"
              fi
              )
              [ $? -eq 0 ] || RC=1
            ''
          ) cfg.config.indexers}
          set -e

          echo "Prowlarr indexers configuration complete"
          exit "$RC"
        '';
      };
}
