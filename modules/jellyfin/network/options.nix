{ config, lib, ... }:
with lib;
let
  secrets = import ../../../lib/secrets { inherit lib; };
in
{
  options.nixflix.jellyfin = {

    openFirewall = mkOption {
      type = types.bool;
      default = false;
      description = ''
        Open ports in the firewall for Jellyfin.
        TCP ports 8096 and 8920, UDP ports 1900 and 7359.
      '';
    };

    subdomain = mkOption {
      type = types.str;
      default = "jellyfin";
      description = "Subdomain prefix for reverse proxy.";
    };

    reverseProxy = {
      expose = mkOption {
        type = types.bool;
        default = true;
        description = "Whether to expose Jellyfin via the reverse proxy.";
      };
    };

    vpn = {
      enable = mkOption {
        type = types.bool;
        default = false;
        defaultText = literalExpression "config.nixflix.vpn.enable";
        description = ''
          Whether to route Jellyfin traffic through the VPN.

          When `false`, Jellyfin bypasses the VPN.
          When `true`, Jellyfin is confined to the WireGuard network namespace (requires nixflix.vpn.enable = true).
        '';
      };
    };

    connectionAddress = mkOption {
      type = types.str;
      readOnly = true;
      default =
        if config.nixflix.jellyfin.network.localNetworkAddresses == [ ] then
          "127.0.0.1"
        else
          builtins.head config.nixflix.jellyfin.network.localNetworkAddresses;
      description = "Address for connecting to this service.";
    };

    network = {
      autoDiscovery = mkOption {
        type = types.bool;
        default = true;
        description = "Enable auto-discovery";
      };

      baseUrl = mkOption {
        type = types.str;
        default = "";
        example = "jellyfin";
        description = "Base URL for Jellyfin (URL prefix) http://localhost:8096/<baseUrl>";
      };

      certificatePassword = secrets.mkSecretOption {
        default = "";
        description = "Certificate password.";
      };

      certificatePath = mkOption {
        type = types.oneOf [
          types.str
          types.path
        ];
        default = "";
        description = "Path to certificate file";
      };

      enableHttps = mkOption {
        type = types.bool;
        default = false;
        description = "Enable HTTPS";
      };

      enableIPv4 = mkOption {
        type = types.bool;
        default = true;
        description = "Enable IPv4";
      };

      enableIPv6 = mkOption {
        type = types.bool;
        default = false;
        description = "Enable IPv6";
      };

      enablePublishedServerUriByRequest = mkOption {
        type = types.bool;
        default = false;
        description = "Enable published server URI by request";
      };

      enableRemoteAccess = mkOption {
        type = types.bool;
        default = true;
        description = "Enable remote access. (Disabling this can cause unexpected authentication failures)";
      };

      enableUPnP = mkOption {
        type = types.bool;
        default = false;
        description = "Enable UPnP";
      };

      ignoreVirtualInterfaces = mkOption {
        type = types.bool;
        default = true;
        description = "Ignore virtual interfaces";
      };

      internalHttpPort = mkOption {
        type = types.ints.between 0 65535;
        default = 8096;
        description = "Internal HTTP port";
      };

      internalHttpsPort = mkOption {
        type = types.ints.between 0 65535;
        default = 8920;
        description = "Internal HTTPS port";
      };

      isRemoteIPFilterBlacklist = mkOption {
        type = types.bool;
        default = false;
        description = "Is remote IP filter a blacklist";
      };

      knownProxies = mkOption {
        type = types.listOf types.str;
        default = if config.nixflix.reverseProxy.enable then [ "127.0.0.1" ] else [ ];
        description = "List of IP addresses or hostnames of known proxies used when connecting to your Jellyfin instance. This is required to make proper use of 'X-Forwarded-For' headers.";
      };

      localNetworkAddresses = mkOption {
        type = types.listOf types.str;
        default =
          if config.nixflix.vpn.enable && config.nixflix.jellyfin.vpn.enable then
            [ config.vpnNamespaces.wg.namespaceAddress ]
          else if config.nixflix.reverseProxy.enable && !config.nixflix.jellyfin.vpn.enable then
            [ "127.0.0.1" ]
          else
            [ ];
        defaultText = literalExpression ''
          if (config.nixflix.reverseProxy.enable && !config.nixflix.jellyfin.vpn.enable)
          then [ "127.0.0.1" ]
          else [ ]
        '';
        description = "Override the local IP address for the HTTP server. If left empty, the server will bind to all available addresses.";
      };

      localNetworkSubnets = mkOption {
        type = types.listOf types.str;
        default = [ ];
        description = "IP addresses or IP/netmask entries for networks that will be considered on local network when enforcing bandwidth and remote access restrictions. If left blank, all RFC1918 addresses are considered local.";
      };

      publicHttpPort = mkOption {
        type = types.ints.between 0 65535;
        default = 8096;
        description = "Public HTTP port";
      };

      publicHttpsPort = mkOption {
        type = types.ints.between 0 65535;
        default = 8920;
        description = "Public HTTPS port";
      };

      publishedServerUriBySubnet = mkOption {
        type = types.listOf types.str;
        default = [ ];
        description = "List of published server URIs by subnet";
      };

      remoteIpFilter = mkOption {
        type = types.listOf types.str;
        default = [ ];
        description = "Remote IP filter list";
      };

      requireHttps = mkOption {
        type = types.bool;
        default = false;
        description = "Require HTTPS";
      };

      virtualInterfaceNames = mkOption {
        type = types.listOf types.str;
        default = [ "veth" ];
        description = "List of virtual interface names";
      };
    };
  };

  config.assertions = [
    {
      assertion = config.nixflix.jellyfin.vpn.enable -> config.nixflix.vpn.enable;
      message = "Cannot enable VPN routing for Jellyfin (nixflix.jellyfin.vpn.enable = true) when VPN is not enabled. Please set nixflix.vpn.enable = true.";
    }
  ];
}
